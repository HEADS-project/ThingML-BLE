import "_Timer.thingml"
import "HouseMessages.thingml"

object ArduinoServo
	@c_type "Servo"
	@c_byte_size "2";

thing Door includes HouseMsgs, TimerMsgs 
@c_header "#include <Servo.h>"
{
	
	readonly property ID 		: UInt8 = 1		// The ID of this door in the house
	readonly property servo_pin	: UInt8 = 9		// The Arduino pin to which the servo is connected
	readonly property vOPEN 	: UInt8 = 130	// The servo value for the door to be open
	readonly property vCLOSE 	: UInt8 = 40	// The servo value for the door to be close
	
	property servo : ArduinoServo
	
	provided port cmds {
		receives openDoor, closeDoor
	}
	
	required port clock {
		receives clock_25ms
	}
	
	statechart DoorSC init CLOSING {
		
		property servo_value : UInt8 = vOPEN
		
		state CLOSING {
			on entry do 
				''&servo&'.attach('&servo_pin&');'
				''&servo&'.write('&servo_value&');'
			end
			on exit ''&servo&'.detach();'
			
			internal event clock?clock_25ms guard servo_value > vCLOSE action do
				servo_value = servo_value - 2
				''&servo&'.write('&servo_value&');'
			end
			
			transition -> CLOSED guard servo_value <= vCLOSE
			transition -> OPENING event c : cmds?openDoor guard (c.id == ID or c.id == 0) 
		}
		
		state CLOSED {
			transition -> OPENING event c : cmds?openDoor guard (c.id == ID or c.id == 0) 
		}
		
		state OPENING {
			on entry ''&servo&'.attach('&servo_pin&');'
			on exit ''&servo&'.detach();'
			
			internal event clock?clock_25ms guard servo_value < vOPEN action do
				servo_value = servo_value + 2
				''&servo&'.write('&servo_value&');'
			end
			
			transition -> OPENED guard servo_value >= vOPEN
			transition -> CLOSING event c : cmds?closeDoor guard (c.id == ID or c.id == 0) 
		}
		
		state OPENED {
			transition -> CLOSING event c : cmds?closeDoor guard (c.id == ID or c.id == 0) 
		}
		
	}
}

thing Light includes HouseMsgs {
	
	provided port cmds {
		receives setLight
	}
	
	readonly property ID 			: UInt8 = 1		// The ID of this light in the house
	readonly property digital_pin	: UInt8 = 2		// The Arduino pin controlling the light
	
	
	statechart LightSC init OFF {
		
		on entry 'pinMode('&digital_pin&', OUTPUT);'
		
		state OFF {
			on entry 'digitalWrite('&digital_pin&', LOW);'
			transition -> ON event c : cmds?setLight guard (c.id == ID or c.id == 0) and c.v > 0
		}
		
		state ON {
			on entry 'digitalWrite('&digital_pin&', HIGH);'
			transition -> OFF event c : cmds?setLight guard (c.id == ID or c.id == 0) and c.v == 0
		}	
	}
}

thing House includes HouseMsgs {
	
	provided port housecontrol {
		receives serSetLight, serOpenDoor, serCloseDoor
		sends serAcknowledge
	}
	
	required port housebus {
		sends setLight, openDoor, closeDoor
	}
	
	statechart House init Ready {
		state Ready {
			internal event c : housecontrol?serSetLight action do
				housecontrol!serAcknowledge(1)
				housebus!setLight(c.id, c.v)
			end
			internal event c : housecontrol?serOpenDoor action do
				housecontrol!serAcknowledge(2)
				housebus!openDoor(c.id)
			end
			internal event c : housecontrol?serCloseDoor action do
				housecontrol!serAcknowledge(3)
				housebus!closeDoor(c.id)
			end
		}
	}
}

protocol Timer;

configuration HouseArduinoCfg {
	
	instance bedroomLight : Light
		set bedroomLight.ID 			= 1
		set bedroomLight.digital_pin 	= 2
		
	instance upstairsLight : Light
		set upstairsLight.ID 			= 2
		set upstairsLight.digital_pin 	= 3
		
	instance downstairsLight : Light
		set downstairsLight.ID 			= 3
		set downstairsLight.digital_pin = 4
		
	instance bathroomLight : Light
		set bathroomLight.ID 			= 4
		set bathroomLight.digital_pin 	= 5
		
	instance bedroomDoor : Door
		set bedroomDoor.ID				= 1
		set bedroomDoor.servo_pin		= 9
		set bedroomDoor.vOPEN			= 135
		set bedroomDoor.vCLOSE			= 25
	connector bedroomDoor.clock over Timer
		
	instance bathroomDoor : Door
		set bathroomDoor.ID				= 2
		set bathroomDoor.servo_pin		= 10
		set bathroomDoor.vOPEN			= 130
		set bathroomDoor.vCLOSE			= 40
	connector bathroomDoor.clock over Timer
		
	instance house : House
	
	connector house.housebus => bedroomLight.cmds
	connector house.housebus => upstairsLight.cmds
	connector house.housebus => downstairsLight.cmds
	connector house.housebus => bathroomLight.cmds
	connector house.housebus => bedroomDoor.cmds
	connector house.housebus => bathroomDoor.cmds
	
	connector house.housecontrol over Serial

}
